apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'


dependencies {
    compile "junit:junit:4.10"
    compile 'javax.json:javax.json-api:1.0'
    compile 'org.glassfish:javax.json:1.0.4'
    compile "org.reflections:reflections:0.9.11"
    compile "com.fasterxml.jackson.core:jackson-databind:2.8.6"

    testCompile 'org.mockito:mockito-all:1.9.5'
    testCompile "org.codehaus.groovy:groovy-all:2.2.0"
}

sourceSets {
    functionalTest {
        compileClasspath = sourceSets.main.output + configurations.testRuntime
        runtimeClasspath = output + sourceSets.main.output + configurations.testRuntime
        java {
            srcDir 'src/functional-test/java'
        }
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task functionalTest(type: Test) {
    testClassesDir = sourceSets.functionalTest.output.classesDir
    classpath = sourceSets.functionalTest.runtimeClasspath
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}


idea {
    module {
        testSourceDirs += file('src/functional-test/java')
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment {
                MavenDeployment deployment -> signing.signPom(deployment)
            }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name = 'Cascade'
                packaging = 'jar'
                description = 'Cascade is a blackbox testing framework'
                url 'http://www.cascadetesting.tech/'

                scm {
                    url 'https://github.com/robindevilliers/cascade'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'robindevilliers'
                        name = 'Robin de Villiers'
                        email 'robindevilliers@me.com'
                    }
                }
            }
        }
    }
}